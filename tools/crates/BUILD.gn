# Copyright (c) 2025 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("sources.gni")

cargo_exe = "//third_party/rust-toolchain/bin/cargo"
if (host_os == "win") {
  cargo_exe += ".exe"
}

action("build_cargo_audit") {
  script = "//build/gn_run_binary.py"

  cargo_audit_src = "cargo_audit/v0_21/crate"

  inputs = [
    cargo_exe,
    "$cargo_audit_src/Cargo.lock",
  ]

  outputs = [ cargo_audit_exe ]

  args = [
    rebase_path(cargo_exe),
    "build",
    "--release",
    "--manifest-path",
    rebase_path("$cargo_audit_src/Cargo.toml"),
    "--target-dir",
    rebase_path(cargo_audit_target_dir),
  ]
}

action("build_wasm_pack") {
  script = "//build/gn_run_binary.py"

  wasm_pack_src = "wasm_pack/v0_13/crate"

  inputs = [
    cargo_exe,
    "$wasm_pack_src/Cargo.lock",
  ]

  outputs = [ wasm_pack_exe ]

  args = [
    rebase_path(cargo_exe),
    "build",
    "--release",
    "--manifest-path",
    rebase_path("$wasm_pack_src/Cargo.toml"),
    "--target-dir",
    rebase_path(wasm_pack_target_dir),
  ]
}
